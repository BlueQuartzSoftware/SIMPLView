
set(SIMPLViewWidgetsLib_ParameterWidgets_HDRS "")
set(SIMPLViewWidgetsLib_ParameterWidgets_SRCS "")
set(SIMPLViewWidgetsLib_ParameterWidgets_UIS "")


set(SIMPLView_PARAMETER_WIDGETS
    AttributeMatrixCreationWidget
    AttributeMatrixSelectionWidget
    AxisAngleWidget
    BooleanWidget
    ChoiceWidget
    ComparisonSelectionWidget
    DataArrayCreationWidget
    DataArraySelectionWidget
    DataBundleSelectionWidget
    DataContainerArrayProxyWidget
    DataContainerCreationWidget
    DataContainerReaderWidget
    DataContainerSelectionWidget
    DoubleWidget
    DynamicChoiceWidget
    DynamicTableWidget
    FileListInfoWidget
    FloatVec3Widget
    FourthOrderPolynomialWidget
    InputFileWidget
    InputPathWidget
    IntVec3Widget
    IntWidget
    LinkedBooleanWidget
    MultiDataArraySelectionWidget
    OutputFileWidget
    OutputPathWidget
    PreflightUpdatedValueWidget
    RangeWidget
    SecondOrderPolynomialWidget
    SeparatorWidget
    StringWidget
    ThirdOrderPolynomialWidget
)

set(SIMPLView_PARAMETER_WIDGETS_NO_CODEGEN
      PhaseTypeSelectionWidget
      ShapeTypeSelectionWidget
      UnknownWidget
)

set(REGISTER_KNOWN_WIDGETS ${SIMPLViewWidgetsLib_BINARY_DIR}/FilterWidgetManager_RegisterWidgets_tmp.cpp)
set(FILTER_PARAMETER_CODEGEN_FILE ${SIMPLViewWidgetsLib_BINARY_DIR}/Filter_Parameter_CodeGenFactory.cpp)
set(FILTER_PARAMETER_CODEGEN_INCLUDES_FILE ${SIMPLViewWidgetsLib_BINARY_DIR}/Filter_Parameter_CodeGenFactory_Includes.cpp)
set(FILTER_PARAMETER_TYPELIST_FILE ${SIMPLViewWidgetsLib_BINARY_DIR}/Filter_Parameter_TypeList.cpp)
set(FILTER_WIDGET_HEADERS ${SIMPLViewWidgetsLib_BINARY_DIR}/FilterWidgetHeaders_tmp.h)

file(WRITE ${REGISTER_KNOWN_WIDGETS}  "/* This file is automatically generated. DO NOT EDIT this file. It will be over written */\n\n")
file(WRITE ${FILTER_PARAMETER_CODEGEN_FILE}  "/* This file is automatically generated. DO NOT EDIT this file. It will be over written */\n\n")
file(WRITE ${FILTER_PARAMETER_CODEGEN_INCLUDES_FILE}  "/* This file is automatically generated. DO NOT EDIT this file. It will be over written */\n\n")
file(WRITE ${FILTER_PARAMETER_TYPELIST_FILE}  "/* This file is automatically generated. DO NOT EDIT this file. It will be over written */\n\n")
file(WRITE ${FILTER_WIDGET_HEADERS}  "/* This file is automatically generated. DO NOT EDIT this file. It will be over written */\n\n")

file(APPEND  ${FILTER_PARAMETER_TYPELIST_FILE} "QList<QString> list;\n")

set(loopCounter 1)
foreach(FPW ${SIMPLView_PARAMETER_WIDGETS})
  set(SIMPLViewWidgetsLib_Widgets_MOC_HDRS ${SIMPLViewWidgetsLib_Widgets_MOC_HDRS}
    ${SIMPLViewWidgetsLib_SOURCE_DIR}/FilterParameterWidgets/${FPW}.h
    )
  set(SIMPLViewWidgetsLib_ParameterWidgets_SRCS ${SIMPLViewWidgetsLib_ParameterWidgets_SRCS}
    ${SIMPLViewWidgetsLib_SOURCE_DIR}/FilterParameterWidgets/${FPW}.cpp
    )
  set(SIMPLViewWidgetsLib_ParameterWidgets_UIS ${SIMPLViewWidgetsLib_ParameterWidgets_UIS}
    ${SIMPLViewWidgetsLib_SOURCE_DIR}/FilterParameterWidgets/UI_Files/${FPW}.ui
    )

  file(APPEND  ${REGISTER_KNOWN_WIDGETS} "{\n")
  file(APPEND  ${REGISTER_KNOWN_WIDGETS} "  PipelineFilterWidgetFactory<${FPW}>::Pointer factory = PipelineFilterWidgetFactory<${FPW}>::New();\n")
  file(APPEND  ${REGISTER_KNOWN_WIDGETS} "  idManager->addFilterWidgetFactory( \"${FPW}\", factory );\n")
  file(APPEND  ${REGISTER_KNOWN_WIDGETS} "}\n")
  file(APPEND  ${FILTER_WIDGET_HEADERS} "#include \"SIMPLViewWidgetsLib/FilterParameterWidgets/${FPW}.h\"\n")

  file(APPEND  ${FILTER_PARAMETER_CODEGEN_INCLUDES_FILE} "#include \"DevHelper/CodeGenerators/${FPW}CodeGenerator.h\"\n")

  file(APPEND  ${FILTER_PARAMETER_TYPELIST_FILE} "list.push_back(\"${FPW}\");\n")

  if (loopCounter EQUAL 1)
    file(APPEND ${FILTER_PARAMETER_CODEGEN_FILE} "if (fpType == \"${FPW}\") {\n")
  else()
    file(APPEND ${FILTER_PARAMETER_CODEGEN_FILE} "else if (fpType == \"${FPW}\") {\n")
  endif()

  file(APPEND ${FILTER_PARAMETER_CODEGEN_FILE} "  ${FPW}CodeGenerator::Pointer ptr = ${FPW}CodeGenerator::New(humanLabel, propertyName, fpCategory, initValue);\n")
      file(APPEND ${FILTER_PARAMETER_CODEGEN_FILE} "  return ptr;\n}\n\n")

  set(loopCounter loopCounter+1)
endforeach()

foreach(FPW ${SIMPLView_PARAMETER_WIDGETS_NO_CODEGEN})
  set(SIMPLViewWidgetsLib_Widgets_MOC_HDRS ${SIMPLViewWidgetsLib_Widgets_MOC_HDRS}
    ${SIMPLViewWidgetsLib_SOURCE_DIR}/FilterParameterWidgets/${FPW}.h
    )
  set(SIMPLViewWidgetsLib_ParameterWidgets_SRCS ${SIMPLViewWidgetsLib_ParameterWidgets_SRCS}
    ${SIMPLViewWidgetsLib_SOURCE_DIR}/FilterParameterWidgets/${FPW}.cpp
    )
  set(SIMPLViewWidgetsLib_ParameterWidgets_UIS ${SIMPLViewWidgetsLib_ParameterWidgets_UIS}
    ${SIMPLViewWidgetsLib_SOURCE_DIR}/FilterParameterWidgets/UI_Files/${FPW}.ui
    )

  file(APPEND  ${REGISTER_KNOWN_WIDGETS} "{\n")
  file(APPEND  ${REGISTER_KNOWN_WIDGETS} "  PipelineFilterWidgetFactory<${FPW}>::Pointer factory = PipelineFilterWidgetFactory<${FPW}>::New();\n")
  file(APPEND  ${REGISTER_KNOWN_WIDGETS} "  idManager->addFilterWidgetFactory( \"${FPW}\", factory );\n")
  file(APPEND  ${REGISTER_KNOWN_WIDGETS} "}\n")
  file(APPEND  ${FILTER_WIDGET_HEADERS} "#include \"SIMPLViewWidgetsLib/FilterParameterWidgets/${FPW}.h\"\n")
endforeach()

file(APPEND ${FILTER_PARAMETER_CODEGEN_FILE} "else {\n")
file(APPEND ${FILTER_PARAMETER_CODEGEN_FILE} "  FPCodeGenerator::Pointer ptr = FPCodeGenerator::New(humanLabel, propertyName, fpCategory, initValue);\n")
file(APPEND ${FILTER_PARAMETER_CODEGEN_FILE} "  return ptr;\n}\n\n")

file(APPEND  ${FILTER_PARAMETER_TYPELIST_FILE} "qSort(list.begin(), list.end());\nreturn list;\n")

cmpReplaceFileIfDifferent(OLD_FILE_PATH ${SIMPLViewWidgetsLib_BINARY_DIR}/FilterWidgetManager_RegisterWidgets.cpp
                          NEW_FILE_PATH ${SIMPLViewWidgetsLib_BINARY_DIR}/FilterWidgetManager_RegisterWidgets_tmp.cpp)
cmpReplaceFileIfDifferent(OLD_FILE_PATH ${SIMPLViewWidgetsLib_BINARY_DIR}/FilterWidgetHeaders.h
                          NEW_FILE_PATH ${SIMPLViewWidgetsLib_BINARY_DIR}/FilterWidgetHeaders_tmp.h)


# --------------------------------------------------------------------
# Add some support files that still need MOC and UIC to be run on them
set(SIMPLViewWidgetsLib_Widgets_MOC_HDRS
  ${SIMPLViewWidgetsLib_Widgets_MOC_HDRS}
  ${SIMPLViewWidgetsLib_SOURCE_DIR}/FilterParameterWidgets/FilterParameterWidget.h
  ${SIMPLViewWidgetsLib_SOURCE_DIR}/FilterParameterWidgets/ComparisonSelectionTableModel.h
  ${SIMPLViewWidgetsLib_SOURCE_DIR}/FilterParameterWidgets/ComparisonSelectionItemDelegate.h
  ${SIMPLViewWidgetsLib_SOURCE_DIR}/FilterParameterWidgets/DynamicTableItemDelegate.h

)

set(SIMPLViewWidgetsLib_Widgets_HDRS
  ${SIMPLViewWidgetsLib_Widgets_MOC_HDRS}
  ${SIMPLViewWidgetsLib_SOURCE_DIR}/FilterParameterWidgets/FilterParameterWidgetsDialogs.h
  ${SIMPLViewWidgetsLib_SOURCE_DIR}/FilterParameterWidgets/FilterParameterWidgetUtils.hpp
)


set(SIMPLViewWidgetsLib_ParameterWidgets_SRCS
  ${SIMPLViewWidgetsLib_ParameterWidgets_SRCS}
  ${SIMPLViewWidgetsLib_SOURCE_DIR}/FilterParameterWidgets/FilterParameterWidget.cpp
  ${SIMPLViewWidgetsLib_SOURCE_DIR}/FilterParameterWidgets/ComparisonSelectionTableModel.cpp
  ${SIMPLViewWidgetsLib_SOURCE_DIR}/FilterParameterWidgets/ComparisonSelectionItemDelegate.cpp
  ${SIMPLViewWidgetsLib_SOURCE_DIR}/FilterParameterWidgets/DynamicTableItemDelegate.cpp
  ${SIMPLViewWidgetsLib_SOURCE_DIR}/FilterParameterWidgets/FilterParameterWidgetsDialogs.cpp
)
set(SIMPLViewWidgetsLib_ParameterWidgets_UIS
  ${SIMPLViewWidgetsLib_ParameterWidgets_UIS}
)


cmp_IDE_SOURCE_PROPERTIES( "SIMPLViewWidgetsLib/FilterParameterWidgets" "${SIMPLViewWidgetsLib_Widgets_HDRS}" "${SIMPLViewWidgetsLib_ParameterWidgets_SRCS}" "0")

cmp_IDE_GENERATED_PROPERTIES("SIMPLViewWidgetsLib/FilterParameterWidgets/UI_Files" "${SIMPLViewWidgetsLib_ParameterWidgets_UIS}" "")

# --------------------------------------------------------------------
# and finally this will run moc:
QT5_WRAP_CPP( SIMPLViewWidgetsLib_ParameterWidgets_Generated_MOC_SRCS ${SIMPLViewWidgetsLib_Widgets_MOC_HDRS} )
set_source_files_properties( ${SIMPLViewWidgetsLib_ParameterWidgets_Generated_MOC_SRCS} PROPERTIES GENERATED TRUE)
set_source_files_properties( ${SIMPLViewWidgetsLib_ParameterWidgets_Generated_MOC_SRCS} PROPERTIES HEADER_FILE_ONLY TRUE)


set(SIMPLViewWidgetsLib_Widgets_SRCS
  ${SIMPLViewWidgetsLib_Widgets_SRCS}
  ${SIMPLViewWidgetsLib_Widgets_Generated_MOC_SRCS}
)


# -- Run MOC and UIC on the necessary files
# QT5_ADD_RESOURCES( SIMPLViewWidgetsLib_Generated_RC_SRCS "${SIMPLViewProj_SOURCE_DIR}/Documentation/Filters/Generated_FilterDocs.qrc"  )

# --------------------------------------------------------------------
# Continue on with our Qt4 section
QT5_WRAP_UI( SIMPLViewWidgetsLib_ParameterWidgets_Generated_UI_HDRS ${SIMPLViewWidgetsLib_ParameterWidgets_UIS} )

# --------------------------------------------------------------------
#-- Put the Qt generated files into their own group for IDEs
cmp_IDE_SOURCE_PROPERTIES( "Generated/Qt_Moc" "" "${SIMPLViewWidgetsLib_ParameterWidgets_Generated_MOC_SRCS}" "0")
cmp_IDE_SOURCE_PROPERTIES( "Generated/Qt_Uic" "${SIMPLViewWidgetsLib_ParameterWidgets_Generated_UI_HDRS}" "" "0")
#cmp_IDE_SOURCE_PROPERTIES( "Generated/Qt_Qrc" "${SIMPLViewWidgetsLib_Generated_RC_SRCS}" "" "0")




